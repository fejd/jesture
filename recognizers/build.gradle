buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

version = '0.1.0'
group = 'com.jesterlabs.jesture'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['myPublication']

    pkg {
        repo = 'maven'
        name = 'jesture'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/fejd/jesture.git'

        version {
            name = '0.1.0'
            desc = 'Jesture 0.1.0'
            released  = new Date()
        }
    }
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            groupId getGroup()
            artifactId 'recognizers'
            version getVersion()
        }
    }
}

test {
    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
      logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
      logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }

    // Always run tests
    outputs.upToDateWhen { false }
}
